PROCEDURE PRO_RETROATIVO_TURMA_ONLINE(P_DT_COMPETENCIA          IN DATE
                                       ,P_NUM_SEQ_DADOS_PROFESSOR IN VARCHAR2 DEFAULT NULL
                                       ,P_COD_TIPO_CURSO          IN NUMBER DEFAULT NULL
                                       ,P_COD_CAMPUS              IN NUMBER DEFAULT NULL
                                       ,P_IND_TIPO_PROCESSO       IN VARCHAR2 DEFAULT NULL
                                       ,P_IND_TIPO_CONTRATO       IN VARCHAR2 DEFAULT NULL
                                       ,P_COD_USUARIO             IN VARCHAR2
                                       ,P_COMMIT                  IN VARCHAR2
                                       ,P_IND_ERRO                OUT VARCHAR2
                                       ,P_MSG_RETORNO             OUT VARCHAR2
                                       ,P_COD_INSTITUICAO         IN VARCHAR2) IS
    --
    -- P_IND_TIPO_PROCESSO  (G) GERAL
    --                      (P) PARCIAL: CHAMADA ISOLADAMENTE
    --
    -- P_IND_TIPO_CONTRATO  (F) FUNCIONÁRIO
    --                      (S) PRESTADOR
    --                      (P) PESSOA JURÍDICA
    --
    -- P_IND_ERRO           (0) REALIZADO COM SUCESSO
    --                      (1) ERRO PREVISTO
    --                      (2) ERRO NÃO PREVISTO
    --                      (3) ADVERTÊNCIA
    --
    ERR_TRATA_ERRO EXCEPTION;
    ERR_PREVISTO EXCEPTION;
    ERR_NAO_PREVISTO EXCEPTION;
    ERR_ADVERTENCIA EXCEPTION;
    --NOME_PADRAO             CONSTANT VARCHAR2(30) := 'CH';
    V_ERRO_ORACLE VARCHAR2(4000);
    --V_IND_ERRO                       VARCHAR2(1);
    --V_MSG_RETORNO                    VARCHAR2(4000);
    V_TXT_PARAMETROS      VARCHAR2(4000);
    V_POSICAO             PLS_INTEGER;
    V_DESTINO             INTEGER;
    V_CONT_DESTINO_RH     PLS_INTEGER;
    V_CONT_DESTINO_FIN    PLS_INTEGER;
    V_CONT_GRAVADOS_IP    PLS_INTEGER;
    V_QTDE_DIAS_TRAB      NUMBER(10);

    V_QTD_REG_GRAVADOS      PLS_INTEGER;
    V_QTDE_OUTROS_TP_CURSOS PLS_INTEGER;
    V_DIA_INI               PLS_INTEGER;
    V_DIA_FIM               PLS_INTEGER;
    V_PROXIMO_CICLO         PLS_INTEGER;
    V_QTDE_HORA_MOVIMENTO   NUMBER(12, 2);
    V_VALOR_MOVIMENTO       NUMBER(12, 2);

    V_ACHOU                  PLS_INTEGER;
    V_CURSO_EXTENSAO         VARCHAR2(1);
    V_COD_TIPO_RUBRICA       PLS_INTEGER;
    V_COD_INSTITUICAO_FILTRO NUMBER(6, 0);
    V_DT_ULTIMA_GERACAO      DATE;
    V_DT_COMPETENCIA_1       DATE;
    V_DT_COMPETENCIA_2       DATE;
    --
    CURSOR C_AP IS
      SELECT VAP.COD_PROFESSOR
            ,VAP.NUM_SEQ_DADOS_PROFESSOR
            ,VAP.TIPO_CURSO COD_TIPO_CURSO
            ,VAP.DT_INICIO_ALOCACAO
            ,VAP.DT_FIM_ALOCACAO
            ,VAP.HORA_INI
            ,VAP.HORA_FIM
            ,VAP.COD_CAMPUS
            ,VAP.COD_CURSO
            ,VAP.COD_TURNO
            ,NULL AS COD_CURSO_EXTENSAO
            ,VAP.IND_SUBSTITUICAO_PROF
            ,VAP.DT_SUBSTITUICAO_PROF
            ,VAP.NUM_MATRICULA
            ,VAP.NUM_SEQ_PERIODO_ACADEMICO
            ,VAP.PK_TURMA AS NUM_SEQ_TURMA
            ,VAP.NUM_SEQ_ALOCACAO
            ,VAP.IND_TIPO_CONTRATO
            ,VAP.PERCENTUAL_APRIMORAMENTO
            ,VAP.COD_BANCO
            ,VAP.COD_AGENCIA
            ,VAP.COD_AGENCIA_DV
            ,VAP.CONTA_CORRENTE
            ,VAP.CONTA_CORRENTE_DV
            ,VAP.COD_CATEGORIA_PROFESSOR
            ,VAP.CPF_PROFESSOR
            ,VAP.IND_ENSINO_DISTANCIA
            ,VAP.CARGA_SEMANAL
            ,VAP.CARGA_SEMANAL_GC
            ,VAP.NUM_SEQ_COMPOSICAO
            ,VAP.HH_INICIO_AULA
            ,VAP.HH_FIM_AULA
            ,VAP.VAL_HORA_AULA
            ,VAP.COD_INSTITUICAO
            ,V_DT_COMPETENCIA_1  AS DT_COMPETENCIA
            ,TU.QTD_HORAS_TEORICO
        FROM SIA.V_ALOCACAO_PROFESSOR VAP
            ,SIA.TURMA TU
       WHERE VAP.PK_TURMA = TU.NUM_SEQ_TURMA
         AND V_DT_COMPETENCIA_1 BETWEEN TRUNC(VAP.DT_INICIO_ALOCACAO,'MM')
                                    AND TRUNC(VAP.DT_FIM_ALOCACAO,'MM')
         AND NVL(VAP.IND_SUBSTITUICAO_PROF, 'N') = 'N'
         AND NVL(VAP.QTD_ALUNOS_MATRICULADOS, 0) > 0
         AND (P_NUM_SEQ_DADOS_PROFESSOR IS NULL OR VAP.NUM_SEQ_DADOS_PROFESSOR = P_NUM_SEQ_DADOS_PROFESSOR)
         AND (P_COD_TIPO_CURSO IS NULL OR VAP.TIPO_CURSO = P_COD_TIPO_CURSO)
         AND (P_COD_CAMPUS IS NULL OR VAP.COD_CAMPUS = P_COD_CAMPUS)
         AND VAP.COD_INSTITUICAO = P_COD_INSTITUICAO
         AND VAP.DT_INCLUSAO     > V_DT_ULTIMA_GERACAO
         AND VAP.DT_GERACAO_PAGAMENTO IS NULL
         AND VAP.IND_PAGAMENTO = 'S' -- SELECIONA APENAS AS TURMAS COM INDICAÇÃO DE PAGAMENTO
         --
         AND NVL(VAP.IND_PAGAMENTO_ALOCACAO,'S') = 'S' -- SELECIONA APENAS AS ALOCAÇÕES COM INDICAÇÃO DE PAGAMENTO
         --
         AND (P_IND_TIPO_CONTRATO IS NULL OR VAP.IND_TIPO_CONTRATO = P_IND_TIPO_CONTRATO)
            --
         AND VAP.TABELA = 'HTV' -- TURMA VIRTUAL
         --
         AND VAP.TIPO_CURSO IN (4,11) -- TIPOS DE CURSO GRADUAÇÃO E GRADUAÇÃO TECNOLÓGICA - SRD 1647
         --
         -- BLOQUEIO DE PERMISSÃO DE PAGAMENTO PARA UMA DETERMINADA COMPETÊNCIA (MALUQUINHA)
         AND NOT EXISTS (SELECT 1
                           FROM SIA.VIGENCIA_BLOQUEIO_PAGAMENTO VP
                          WHERE VP.COD_TIPO_CURSO = VAP.TIPO_CURSO
                            AND VP.COD_INSTITUICAO = P_COD_INSTITUICAO
                            AND TRUNC(P_DT_COMPETENCIA, 'MM') BETWEEN TRUNC(VP.DT_INI_VIGENCIA,'MM')
                                                                  AND TRUNC(VP.DT_FIM_VIGENCIA,'MM')
                            AND ((VP.COD_CURSO IS NULL AND VAP.COD_CURSO = VAP.COD_CURSO) OR (VP.COD_CURSO IS NOT NULL AND VAP.COD_CURSO = VP.COD_CURSO)))
         --
         -- PAGAMENTO BLOQUEADO PARA O PROFESSOR
         AND NOT EXISTS (SELECT 1
                           FROM SIA.BLOQUEIO_PAGAMENTO_RETROATIVO BLOQ
                          WHERE BLOQ.COD_PROFESSOR = VAP.COD_PROFESSOR
                            AND BLOQ.NUM_SEQ_ALOCACAO IS NULL
                            AND BLOQ.DT_MES_ANO_COMPETENCIA = TRUNC(P_DT_COMPETENCIA,'MM'))
         --
         -- PAGAMENTO BLOQUEADO PARA A ALOCAÇÃO
         AND NOT EXISTS (SELECT 1
                           FROM SIA.BLOQUEIO_PAGAMENTO_RETROATIVO BLOQ
                          WHERE BLOQ.NUM_SEQ_ALOCACAO = VAP.NUM_SEQ_ALOCACAO
                            AND BLOQ.COD_PROFESSOR    = VAP.COD_PROFESSOR
                            AND BLOQ.DT_MES_ANO_COMPETENCIA = TRUNC(P_DT_COMPETENCIA,'MM'))
         --
         -- PAGAMAMENTO APENAS DOS DOCENTES HORISTAS.
         AND VAP.IND_TIPO_SALARIO = 'H'
         --
         -- BLOQUEIO DE PAGAMAMENTO DOS DOCENTES QUE SÃO ADMINISTRATIVOS NO RH.
         AND NOT EXISTS (SELECT 1
                           FROM SIA.ADMINISTRATIVO_RH ARH
                          WHERE ARH.NUM_MATRICULA = VAP.NUM_MATRICULA)
-- SRD 1705-Importação CH Docente SIA x ADP (FAL e FATERN) - NÃO PAGAR RETROATIVO
AND VAP.COD_INSTITUICAO NOT IN (1022,1809,1464) 
--
      --
      UNION ALL
      --
      SELECT VAP.COD_PROFESSOR
            ,VAP.NUM_SEQ_DADOS_PROFESSOR
            ,VAP.TIPO_CURSO COD_TIPO_CURSO
            ,VAP.DT_INICIO_ALOCACAO
            ,VAP.DT_FIM_ALOCACAO
            ,VAP.HORA_INI
            ,VAP.HORA_FIM
            ,VAP.COD_CAMPUS
            ,VAP.COD_CURSO
            ,VAP.COD_TURNO
            ,NULL AS COD_CURSO_EXTENSAO
            ,VAP.IND_SUBSTITUICAO_PROF
            ,VAP.DT_SUBSTITUICAO_PROF
            ,VAP.NUM_MATRICULA
            ,VAP.NUM_SEQ_PERIODO_ACADEMICO
            ,VAP.PK_TURMA AS NUM_SEQ_TURMA
            ,VAP.NUM_SEQ_ALOCACAO
            ,VAP.IND_TIPO_CONTRATO
            ,VAP.PERCENTUAL_APRIMORAMENTO
            ,VAP.COD_BANCO
            ,VAP.COD_AGENCIA
            ,VAP.COD_AGENCIA_DV
            ,VAP.CONTA_CORRENTE
            ,VAP.CONTA_CORRENTE_DV
            ,VAP.COD_CATEGORIA_PROFESSOR
            ,VAP.CPF_PROFESSOR
            ,VAP.IND_ENSINO_DISTANCIA
            ,VAP.CARGA_SEMANAL
            ,VAP.CARGA_SEMANAL_GC
            ,VAP.NUM_SEQ_COMPOSICAO
            ,VAP.HH_INICIO_AULA
            ,VAP.HH_FIM_AULA
            ,VAP.VAL_HORA_AULA
            ,VAP.COD_INSTITUICAO
            ,V_DT_COMPETENCIA_2  AS DT_COMPETENCIA
            ,TU.QTD_HORAS_TEORICO
        FROM SIA.V_ALOCACAO_PROFESSOR VAP
            ,SIA.TURMA TU
       WHERE VAP.PK_TURMA = TU.NUM_SEQ_TURMA
         AND V_DT_COMPETENCIA_2 BETWEEN TRUNC(VAP.DT_INICIO_ALOCACAO,'MM')
                                    AND TRUNC(VAP.DT_FIM_ALOCACAO,'MM')
         AND NVL(VAP.IND_SUBSTITUICAO_PROF, 'N') = 'N'
         AND NVL(VAP.QTD_ALUNOS_MATRICULADOS, 0) > 0
         AND (P_NUM_SEQ_DADOS_PROFESSOR IS NULL OR VAP.NUM_SEQ_DADOS_PROFESSOR = P_NUM_SEQ_DADOS_PROFESSOR)
         AND (P_COD_TIPO_CURSO IS NULL OR VAP.TIPO_CURSO = P_COD_TIPO_CURSO)
         AND (P_COD_CAMPUS IS NULL OR VAP.COD_CAMPUS = P_COD_CAMPUS)
         AND VAP.COD_INSTITUICAO = P_COD_INSTITUICAO
         AND VAP.DT_INCLUSAO     > V_DT_ULTIMA_GERACAO
         AND VAP.DT_GERACAO_PAGAMENTO IS NULL
         AND VAP.IND_PAGAMENTO = 'S' -- SELECIONA APENAS AS TURMAS COM INDICAÇÃO DE PAGAMENTO
         AND (P_IND_TIPO_CONTRATO IS NULL OR VAP.IND_TIPO_CONTRATO = P_IND_TIPO_CONTRATO)
         --
         AND VAP.TABELA = 'HTV' -- TURMA VIRTUAL
         --
         AND VAP.TIPO_CURSO IN (4,11) -- TIPOS DE CURSO GRADUAÇÃO E GRADUAÇÃO TECNOLÓGICA
         --
         -- BLOQUEIO DE PERMISSÃO DE PAGAMENTO PARA UMA DETERMINADA COMPETÊNCIA (MALUQUINHA)
         AND NOT EXISTS (SELECT 1
                           FROM SIA.VIGENCIA_BLOQUEIO_PAGAMENTO VP
                          WHERE VP.COD_TIPO_CURSO = VAP.TIPO_CURSO
                            AND VP.COD_INSTITUICAO = P_COD_INSTITUICAO
                            AND TRUNC(P_DT_COMPETENCIA, 'MM') BETWEEN TRUNC(VP.DT_INI_VIGENCIA,'MM')
                                                                  AND TRUNC(VP.DT_FIM_VIGENCIA,'MM')
                            AND ((VP.COD_CURSO IS NULL AND VAP.COD_CURSO = VAP.COD_CURSO) OR (VP.COD_CURSO IS NOT NULL AND VAP.COD_CURSO = VP.COD_CURSO)))
         --
         -- PAGAMENTO BLOQUEADO PARA O PROFESSOR
         AND NOT EXISTS (SELECT 1
                           FROM SIA.BLOQUEIO_PAGAMENTO_RETROATIVO BLOQ
                          WHERE BLOQ.COD_PROFESSOR = VAP.COD_PROFESSOR
                            AND BLOQ.NUM_SEQ_ALOCACAO IS NULL
                            AND BLOQ.DT_MES_ANO_COMPETENCIA = TRUNC(P_DT_COMPETENCIA,'MM'))
         --
         -- PAGAMENTO BLOQUEADO PARA A ALOCAÇÃO
         AND NOT EXISTS (SELECT 1
                           FROM SIA.BLOQUEIO_PAGAMENTO_RETROATIVO BLOQ
                          WHERE BLOQ.NUM_SEQ_ALOCACAO = VAP.NUM_SEQ_ALOCACAO
                            AND BLOQ.COD_PROFESSOR    = VAP.COD_PROFESSOR
                            AND BLOQ.DT_MES_ANO_COMPETENCIA = TRUNC(P_DT_COMPETENCIA,'MM'))
         --
         -- PAGAMAMENTO APENAS DOS DOCENTES HORISTAS.
         AND VAP.IND_TIPO_SALARIO = 'H'
         --
         -- BLOQUEIO DE PAGAMAMENTO DOS DOCENTES QUE SÃO ADMINISTRATIVOS NO RH.
         AND NOT EXISTS (SELECT 1
                           FROM SIA.ADMINISTRATIVO_RH ARH
                          WHERE ARH.NUM_MATRICULA = VAP.NUM_MATRICULA)
-- SRD 1705-Importação CH Docente SIA x ADP (FAL e FATERN) - NÃO PAGAR RETROATIVO
AND VAP.COD_INSTITUICAO NOT IN (1022,1809,1464) 
--
       --
       ORDER BY DT_COMPETENCIA
               ,NUM_SEQ_DADOS_PROFESSOR
               ,COD_PROFESSOR
               ,NUM_SEQ_ALOCACAO;
    --
    R_AP C_AP%ROWTYPE;
    --
    --
  BEGIN
    --
    BEGIN
      --
      BEGIN
        --
        P_IND_ERRO    := '0';
        P_MSG_RETORNO := '';
        V_QTD_REG_GRAVADOS      := 0;
        V_QTDE_OUTROS_TP_CURSOS := 0;
        V_DESTINO               := 1;
        V_CONT_DESTINO_RH       := 0;
        V_CONT_DESTINO_FIN      := 0;
        V_CONT_GRAVADOS_IP      := 0;
        --
        V_TXT_PARAMETROS := ' P_DT_COMPETENCIA   : ' || TO_CHAR(P_DT_COMPETENCIA, 'MM/YYYY') || CHR(13) || ' P_NUM_SEQ_DADOS_PROFESSOR : ' || P_NUM_SEQ_DADOS_PROFESSOR || CHR(13) || ' P_COD_TIPO_CURSO   : ' || P_COD_TIPO_CURSO || CHR(13) || ' P_COD_CAMPUS       : ' || P_COD_CAMPUS || CHR(13) ||
                            ' P_IND_TIPO_PROCESSO: ' || P_IND_TIPO_PROCESSO || CHR(13) || ' P_IND_TIPO_CONTRATO: ' || P_IND_TIPO_CONTRATO || CHR(13) || ' P_COD_INSTITUICAO  : ' || P_COD_INSTITUICAO;
        --
        -- 1. VALIDA PARÂMETROS OBRIGATÓRIOS
        --
        V_POSICAO := 10;
        --
        IF P_DT_COMPETENCIA IS NULL THEN
          P_MSG_RETORNO := 'A DATA DE COMPETÊNCIA DEVE SER INFORMADA.';
          RAISE ERR_PREVISTO;
        END IF;
        --
        IF P_IND_TIPO_PROCESSO IS NULL THEN
          P_MSG_RETORNO := 'O TIPO DE PROCESSO DEVE SER INFORMADO.';
          RAISE ERR_PREVISTO;
        END IF;
        --
        V_POSICAO := 20;
        --
        BEGIN
          --
          SELECT 1
            INTO V_ACHOU
            FROM SIA.INTERFACE_ARQUIVO
           WHERE NOM_PROCESSO = 'PRO_RETROATIVO_TURMA_ONLINE';
          --
        EXCEPTION
          WHEN NO_DATA_FOUND THEN
            P_MSG_RETORNO := 'O PROCESSO "PRO_RETROATIVO_TURMA_ONLINE" DEVE SER CADASTRO NA TABELA DE INTERFACE DE ARQUIVOS.';
            RAISE ERR_PREVISTO;
        END;
        --
        -- BUSCA DIA DE GERAÇÃO DO MES ANTERIOR - PRO_APURACAO_ALOCACAO
        BEGIN
          --
          SELECT MAX(TRUNC(CPP.DT_INICIO_GERACAO))
            INTO V_DT_ULTIMA_GERACAO
            FROM SIA.CONTROLE_PAGAMENTO_PROFESSOR CPP
           WHERE CPP.NOM_PROCESSO = 'PRO_APURACAO_TURMA_ONLINE'
             AND CPP.COD_INSTITUICAO = P_COD_INSTITUICAO;
          --
        EXCEPTION
        WHEN NO_DATA_FOUND THEN
            P_MSG_RETORNO := 'NENHUMA INFORMAÇÃO FOI ENCONTRADA PARA O PERÍODOS ANTERIORES, NO PROCESSO DE TURMA ONLINE.';
            RAISE ERR_ADVERTENCIA;
        END;

        IF V_DT_ULTIMA_GERACAO IS NULL THEN
            P_MSG_RETORNO := 'NENHUMA INFORMAÇÃO FOI ENCONTRADA PARA O PERÍODOS ANTERIORES, NO PROCESSO DE TURMA ONLINE.';
            RAISE ERR_ADVERTENCIA;
        END IF;

        V_DT_COMPETENCIA_1 := ADD_MONTHS(TRUNC(P_DT_COMPETENCIA,'MM'),-1);
        V_DT_COMPETENCIA_2 := ADD_MONTHS(TRUNC(P_DT_COMPETENCIA,'MM'),-2);
        --
        -- 3. ABRE O CURSOR E CARREGA AS LINHAS NO REGISTRO ANTERIOR (R_AP_ANT)
        --
        V_POSICAO := 30;
        --
        OPEN C_AP;
        FETCH C_AP
          INTO R_AP;
        --
        IF C_AP%NOTFOUND 
        THEN
          P_MSG_RETORNO := 'NENHUMA INFORMAÇÃO FOI ENCONTRADA PARA O PERÍODO ' || TO_CHAR(P_DT_COMPETENCIA, 'MM/YYYY') || ' NO PROCESSO DE TURMA ONLINE.';
          RAISE ERR_ADVERTENCIA;
        END IF;
        --
        BEGIN
          --
          -- 4. OBTEM O PRÓXIMO CICLO DENTRO DE UMA COMPETÊNCIA.
          --
          V_POSICAO := 40;
          --
          SELECT NVL(MAX(CICLO), 0) + 1
            INTO V_PROXIMO_CICLO
            FROM SIA.A_CONTROLE_PAGAMENTO_PROFESSOR
           WHERE DT_MES_ANO_COMPETENCIA = P_DT_COMPETENCIA
             AND COD_INSTITUICAO = P_COD_INSTITUICAO
             AND NOM_PROCESSO = 'PRO_RETROATIVO_TURMA_ONLINE';
          --
        EXCEPTION
        WHEN OTHERS THEN
            P_MSG_RETORNO := 'FALHA AO RECUPERAR O PRÓXIMO CICLO DE INTERFACE PAGAMENTO.';
            V_ERRO_ORACLE := SQLERRM;
            RAISE ERR_NAO_PREVISTO;
        END;
        --
        WHILE C_AP%FOUND
        LOOP
          --
          -- ATUALIZA CONTADORES
          --
          V_POSICAO := 50;
          --
          IF R_AP.IND_TIPO_CONTRATO IN ('F', 'S') 
          THEN
            V_DESTINO         := 1;
            V_CONT_DESTINO_RH := V_CONT_DESTINO_RH + 1;
          ELSIF R_AP.IND_TIPO_CONTRATO = 'P' 
          THEN
            V_DESTINO          := 2;
            V_CONT_DESTINO_FIN := V_CONT_DESTINO_FIN + 1;
          END IF;
          --
          -- PROPORCIONAL
          --
          V_POSICAO := 60;
          --
          --
          IF TRUNC(R_AP.DT_INICIO_ALOCACAO, 'MM') = TRUNC(R_AP.DT_COMPETENCIA, 'MM') 
          THEN
            --
            V_DIA_INI := TO_NUMBER(TO_CHAR(R_AP.DT_INICIO_ALOCACAO, 'DD'));
            --
          ELSE
            --
            V_DIA_INI := 01;
            --
          END IF; -- IF TRUNC(R_AP.DT_INICIO_ATUACAO,'MM') = TRUNC(P_DT_COMPETENCIA,'MM') THEN
          --
          IF TRUNC(R_AP.DT_FIM_ALOCACAO, 'MM') = TRUNC(R_AP.DT_COMPETENCIA, 'MM') 
          THEN
            --
            V_DIA_FIM := TO_NUMBER(TO_CHAR(R_AP.DT_FIM_ALOCACAO, 'DD'));
            --
            IF V_DIA_FIM = TO_NUMBER(TO_CHAR(LAST_DAY(R_AP.DT_FIM_ALOCACAO), 'DD')) 
            THEN
              --
              V_DIA_FIM := 30;
              --
            END IF;
            --
          ELSE
            --
            V_DIA_FIM := 30;
            --
          END IF; -- IF TRUNC(R_AP.DT_FIM_ATUACAO,'MM') = TRUNC(P_DT_COMPETENCIA,'MM') THEN
          --
          V_QTDE_DIAS_TRAB := V_DIA_FIM - V_DIA_INI + 1;
          --
          --
          V_VALOR_MOVIMENTO     := 0;
          V_QTDE_HORA_MOVIMENTO := 0;
          -- SRD - 1436-Pagamento de Graduacao EAD.
          V_QTDE_HORA_MOVIMENTO := ROUND((1 / 30) * V_QTDE_DIAS_TRAB, 2);
          --
          SELECT DECODE(R_AP.COD_CURSO_EXTENSAO, NULL, 'N', 'S')
            INTO V_CURSO_EXTENSAO
            FROM DUAL;
          --
          -- REQ. 4893 - CÁLCULO DE HORAS TRABALHADAS
          --
          -- ATUALIZA A_INTERFACE_PAGAMENTO
          --
          V_POSICAO := 70;
          --
          -- Alterado através da SRD 1647 para utilizar a Rubrica - Curso a Distância (Código 27) 
          V_COD_TIPO_RUBRICA := 27;
          --
          GRAVA_ARQUIVO( V_COD_TIPO_RUBRICA                     -- PI_COD_TIPO_RUBRICA
                       , P_DT_COMPETENCIA                       -- PI_DT_COMPETENCIA           (V_DT_COMPETENCIA)
                       , V_PROXIMO_CICLO                        -- PI_PROXIMO_CICLO
                       , 'PRO_RETROATIVO_TURMA_ONLINE'          -- 'PRO_2_APURACAO_ALOCACAO'          -- PI_NOM_PROCESSO
                       , '1'                                    -- PI_IND_SITUACAO_PAGAMENTO   (1=GERADO)
                       , V_DESTINO                              -- PI_IND_DESTINO
                       , V_CURSO_EXTENSAO                       -- PI_IND_CURSO_EXTENSAO
                       , R_AP.COD_PROFESSOR                     -- PI_COD_PROFESSOR
                       , R_AP.NUM_MATRICULA                     -- PI_NUM_MATRICULA
                       , R_AP.COD_TIPO_CURSO                    -- PI_COD_TIPO_CURSO
                       , R_AP.COD_CURSO                         -- PI_COD_CURSO
                       , NULL                                   -- PI_COD_CURSO_EXTENSAO
                       , R_AP.COD_CAMPUS                        -- PI_COD_CAMPUS
                       , R_AP.COD_TURNO                         -- PI_COD_TURNO
                       , V_QTDE_HORA_MOVIMENTO                  -- PI_QTD_HORAS_MOVIMENTO
                       , NULL                                   -- PI_VALOR_MOVIMENTO
                       , SYSDATE                                -- PI_DT_GERACAO
                       , NULL                                   -- PI_DT_LIBERACAO
                       , NULL                                   -- PI_DT_PROCESSAMENTO
                       , NULL                                   -- PI_NUM_SEQ_SOLICITACAO
                       , R_AP.NUM_SEQ_ALOCACAO                  -- PI_NUM_SEQ_ALOCACAO
                       , NULL                                   -- PI_NUM_SEQ_ATUACAO
                       , NULL                                   -- PI_MES_ANO_ATUACAO
                       , NULL                                   -- PI_NUM_SEQ_FALTA
                       , R_AP.PERCENTUAL_APRIMORAMENTO          -- PI_PERCENTUAL_APRIMORAMENTO
                       , R_AP.COD_BANCO                         -- PI_COD_BANCO
                       , R_AP.COD_AGENCIA                       -- PI_COD_AGENCIA
                       , R_AP.COD_AGENCIA_DV                    -- PI_COD_AGENCIA_DV
                       , R_AP.CONTA_CORRENTE                    -- PI_COD_CONTA_CORRENTE
                       , R_AP.CONTA_CORRENTE_DV                 -- PI_COD_CONTA_CORRENTE_DV
                       , R_AP.COD_CATEGORIA_PROFESSOR           -- PI_COD_CATEGORIA_PROFESSOR
                       , R_AP.NUM_SEQ_TURMA                     -- PI_NUM_SEQ_TURMA
                       , NULL                                   -- PI_COD_TURMA_EXTENSAO
                       , R_AP.IND_TIPO_CONTRATO                 -- PI_IND_TIPO_CONTRATO
                       , P_COD_INSTITUICAO                      -- PI_COD_INSTITUICAO
                       , 'N'                                    -- PI_COMMIT
                       , P_IND_ERRO                             -- PO_IND_ERRO
                       , P_MSG_RETORNO                          -- PO_MSG_RETORNO
                       , NULL                                   -- PI_COD_TIPO_ATUACAO
                       , V_QTDE_HORA_MOVIMENTO                  -- PI_QTD_HORAS_TRABALHADAS -> REQ. 4893 - CÁLCULO DE HORAS TRABALHADAS
                       , R_AP.IND_ENSINO_DISTANCIA              -- PI_IND_ENSINO_DISTANCIA
                       , V_QTDE_DIAS_TRAB                       -- PI_QTD_DIAS_TRABALHADOS
                       , R_AP.QTD_HORAS_TEORICO);               -- PI_QTD_HORAS_TEORICO
          --
          IF P_IND_ERRO <> '0' THEN
            RAISE ERR_PREVISTO;
          END IF;
          --
          V_QTD_REG_GRAVADOS := V_QTD_REG_GRAVADOS + 1;
          --
          IF MOD(V_QTD_REG_GRAVADOS, 1000) = 0 AND
             P_COMMIT = 'S'
          THEN
            COMMIT;
          END IF;
          --
          FETCH C_AP
            INTO R_AP;
          --
        END LOOP;
        --

        --
        --**************************************************************************
        -- ATUALIZA ALOCAÇÃO DE PROFESSOR PARA INDICAÇÃO DE GERAÇÃO DE PAGAMENTO ***
        --**************************************************************************
        BEGIN
          --
          V_POSICAO := 210;
          --
          UPDATE SIA.ALOCACAO_PROFESSOR AP
             SET AP.DT_GERACAO_PAGAMENTO = SYSDATE
           WHERE AP.DT_GERACAO_PAGAMENTO IS NULL
             AND EXISTS (SELECT 1
                    FROM SIA.A_INTERFACE_PAGAMENTO IP
                   WHERE TRUNC(IP.DT_MES_ANO_COMPETENCIA, 'MM') = TRUNC(P_DT_COMPETENCIA, 'MM')
                     AND IP.NUM_SEQ_ALOCACAO = AP.NUM_SEQ_ALOCACAO
                     AND IP.COD_PROFESSOR    = AP.COD_PROFESSOR
                     AND IP.COD_INSTITUICAO  = P_COD_INSTITUICAO
                     AND IP.NOM_PROCESSO     = 'PRO_RETROATIVO_TURMA_ONLINE');
          --
        EXCEPTION
        WHEN OTHERS THEN
            P_MSG_RETORNO := 'ERRO AO ATUALIZAR INDICAÇÃO DE PAGAMENTO NA ALOCAÇÃO.';
            V_ERRO_ORACLE := SQLERRM;
            RAISE ERR_NAO_PREVISTO;
        END;

        --
        P_MSG_RETORNO := 'QTDE DE REGISTROS ENVIADOS AO RH: ' || V_CONT_DESTINO_RH || CHR(13) || 'QTDE DE REGISTROS ENVIADOS AO FIN: ' || V_CONT_DESTINO_FIN || CHR(13) || 'QTDE DE REGISTROS NÃO PERTENCENTES AOS TIPOS DE CURSO GRADUACAO E POLITECNICO: ' || V_QTDE_OUTROS_TP_CURSOS;
        --

        --
        IF ( NVL(V_CONT_DESTINO_RH,0) + NVL(V_CONT_DESTINO_FIN,0) ) > 0 THEN
           --
            BEGIN
              --
              -- INCLUI O SISTÉTICO DO PROCESSO
              --
              BEGIN
                V_COD_INSTITUICAO_FILTRO := TO_NUMBER(P_COD_INSTITUICAO);
              EXCEPTION
                WHEN OTHERS THEN
                  V_COD_INSTITUICAO_FILTRO := NULL;
              END;
              --
              V_POSICAO := 200;
              --
              INSERT INTO SIA.A_CONTROLE_PAGAMENTO_PROFESSOR
                (DT_MES_ANO_COMPETENCIA
                ,NOM_PROCESSO
                ,IND_TIPO_PROCESSO
                 --                      , COD_PROFESSOR
                ,NUM_SEQ_DADOS_PROFESSOR
                ,COD_CAMPUS
                ,COD_TIPO_CURSO
                ,QTD_GRAVADOS_IP
                ,DT_INICIO_GERACAO
                ,DT_FIM_GERACAO
                ,IND_RETORNO
                ,TXT_MSG_PROCESSO
                ,TOTAL_REGS_RUBRICA_1
                ,TOTAL_REGS_RUBRICA_2
                ,TOTAL_HORAS_RUBRICA_1
                ,TOTAL_HORAS_RUBRICA_2
                ,QTD_REGS_DESTINO_1
                ,QTD_REGS_DESTINO_2
                ,CICLO
                ,IND_TIPO_CONTRATO
                ,COD_USUARIO_LOG
                ,DT_INICIO_SELECAO
                ,DT_FIM_SELECAO
                ,COD_INSTITUICAO)
              VALUES
                (P_DT_COMPETENCIA
                ,'PRO_RETROATIVO_TURMA_ONLINE'
                ,P_IND_TIPO_PROCESSO
                 --                      , P_COD_PROFESSOR
                ,P_NUM_SEQ_DADOS_PROFESSOR
                ,P_COD_CAMPUS
                ,P_COD_TIPO_CURSO
                ,V_CONT_GRAVADOS_IP
                ,SYSDATE
                ,SYSDATE
                ,P_IND_ERRO
                ,P_MSG_RETORNO
                ,NULL --V_CONT_RUBRICA_CARGA_HR,
                ,NULL --V_CONT_RUBRICA_AD_NOT,
                ,NULL --V_QTD_CARGA_MENSAL_HR,
                ,NULL --V_QTD_CARGA_MENSAL_AD_NOT,
                ,V_CONT_DESTINO_RH
                ,V_CONT_DESTINO_FIN
                ,V_PROXIMO_CICLO
                ,P_IND_TIPO_CONTRATO
                ,P_COD_USUARIO
                ,TRUNC(ADD_MONTHS(P_DT_COMPETENCIA,-2),'MM')
                ,LAST_DAY(ADD_MONTHS(P_DT_COMPETENCIA,-1))
                ,V_COD_INSTITUICAO_FILTRO);
            EXCEPTION
            WHEN OTHERS THEN
                V_ERRO_ORACLE := SQLERRM;
                P_MSG_RETORNO := 'HOUVE UMA FALHA AO INCLUIR NA TABELA "CONTROLE_PAGAMENTO_PROFESSOR".';
                RAISE ERR_NAO_PREVISTO;
            END;
            --
        END IF;

        V_POSICAO := 300;
        --
        IF P_COMMIT = 'S' THEN
          COMMIT;
        END IF;
        --
        P_IND_ERRO    := '0';
        P_MSG_RETORNO := '';
        --
      EXCEPTION
        --
        WHEN ERR_ADVERTENCIA THEN
          P_IND_ERRO := '3';
        WHEN ERR_PREVISTO THEN
          P_IND_ERRO := '1';
          RAISE ERR_TRATA_ERRO;
        WHEN ERR_NAO_PREVISTO THEN
          P_IND_ERRO := '2';
          RAISE ERR_TRATA_ERRO;
        WHEN OTHERS THEN
          P_MSG_RETORNO := SQLERRM;
          V_ERRO_ORACLE := SQLERRM;
          P_IND_ERRO    := '2';
          RAISE ERR_TRATA_ERRO;
          --
      END;
    EXCEPTION
      WHEN ERR_TRATA_ERRO THEN
        --
        IF P_COMMIT = 'S' THEN
          ROLLBACK;
        END IF;
        --
        IF P_IND_ERRO = '2' THEN
          SEG.SEG_LOG_EXECUCAO('PRO_RETROATIVO_TURMA_ONLINE, IE: ' || R_AP.COD_INSTITUICAO, V_POSICAO, V_ERRO_ORACLE, V_TXT_PARAMETROS, P_MSG_RETORNO);
        END IF;
        --
    END;
    --
    IF C_AP%ISOPEN
    THEN
      CLOSE C_AP;
    END IF;
    --
  END PRO_RETROATIVO_TURMA_ONLINE;